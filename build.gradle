plugins {
    id 'java'
    id 'org.jetbrains.gradle.plugin.idea-ext' version '1.3'
    id 'com.gradleup.shadow' version '9.3.1'
}

import org.gradle.internal.os.OperatingSystem

// ===== Hytale Home Detection =====
def detectHytaleHome() {
    def os = OperatingSystem.current()
    if (os.isWindows()) {
        return "${System.getProperty("user.home")}/AppData/Roaming/Hytale"
    } else if (os.isMacOsX()) {
        return "${System.getProperty("user.home")}/Library/Application Support/Hytale"
    } else if (os.isLinux()) {
        def flatpakPath = "${System.getProperty("user.home")}/.var/app/com.hypixel.HytaleLauncher/data/Hytale"
        def localPath = "${System.getProperty("user.home")}/.local/share/Hytale"
        return file(flatpakPath).exists() ? flatpakPath : localPath
    }
    return null
}

ext.hytaleHome = project.findProperty('hytale_home') ?: detectHytaleHome()
ext.hasHytaleHome = ext.hytaleHome && file(ext.hytaleHome).exists()

if (!ext.hasHytaleHome) {
    logger.lifecycle("Hytale install not detected; run configs that launch the server will be skipped. Set -Phytale_home=/path/to/Hytale to enable them.")
}

// ===== Java Configuration =====
java {
    toolchain.languageVersion = JavaLanguageVersion.of(java_version)
    withSourcesJar()
    withJavadocJar()
}

javadoc {
    options.addStringOption('Xdoclint:-missing', '-quiet')
}

// ===== Configurations =====
configurations {
    shade // Custom configuration for dependencies to be shaded into the JAR
}

// ===== Repositories =====
// ===== Repositories =====
repositories {
    mavenCentral()
    
    maven {
        name = 'hytale-release'
        url = uri('https://maven.hytale.com/release')
    }
    
    maven {
        name = 'hytale-pre-release'
        url = uri('https://maven.hytale.com/pre-release')
    }
    
    maven {
        name = 'jitpack'
        url = uri('https://jitpack.io')
    }
    
    maven {
        name = 'codemc'
        url = uri('https://repo.codemc.io/repository/creatorfromhell/')
    }
}

// ===== Dependencies =====
dependencies {
    // Hytale Server API (not bundled)
    compileOnly "com.hypixel.hytale:Server:$hytale_build"
    testImplementation "com.hypixel.hytale:Server:$hytale_build"
    
    if (project.ext.hasHytaleHome) {
        runtimeOnly files("${project.ext.hytaleHome}/install/$patchline/package/game/latest/Server/HytaleServer.jar")
    }
    
    // Shaded dependencies (bundled into plugin JAR)
    implementation "org.mozilla:rhino:1.7.15"
    implementation "com.fasterxml.jackson.core:jackson-databind:2.17.1"
    implementation "org.xerial:sqlite-jdbc:3.46.0.0"
    
    shade "org.mozilla:rhino:1.7.15"
    shade "com.fasterxml.jackson.core:jackson-databind:2.17.1"
    shade "com.fasterxml.jackson.core:jackson-annotations:2.17.1"
    shade "com.fasterxml.jackson.core:jackson-core:2.17.1"
    shade "org.xerial:sqlite-jdbc:3.46.0.0"
    
    // Test dependencies
    testImplementation "org.junit.jupiter:junit-jupiter:5.10.2"
    testImplementation "org.mockito:mockito-core:5.2.0"
    testImplementation "org.mockito:mockito-inline:5.2.0"
    testRuntimeOnly "org.junit.platform:junit-platform-launcher:1.10.2"
}

// ===== Tasks =====

// Updates the manifest.json with the latest version and asset pack info
tasks.register('updatePluginManifest') {
    def manifestFile = file('src/main/resources/manifest.json')
    
    doLast {
        if (!manifestFile.exists()) {
            throw new GradleException("Could not find manifest.json at ${manifestFile.path}")
        }
        
        def manifestJson = new groovy.json.JsonSlurper().parseText(manifestFile.text)
        manifestJson.Version = version
        manifestJson.IncludesAssetPack = includes_pack.toBoolean()
        manifestFile.text = groovy.json.JsonOutput.prettyPrint(groovy.json.JsonOutput.toJson(manifestJson))
    }
}

processResources {
    dependsOn updatePluginManifest
}

shadowJar {
    archiveClassifier.set('')
    mergeServiceFiles()
    configurations = [project.configurations.shade]
    zip64 = true // Required for large dependencies like SQLite
}

build {
    dependsOn shadowJar
}

test {
    useJUnitPlatform()
    jvmArgs = [
        "-Djava.util.logging.manager=com.hypixel.hytale.logger.backend.HytaleLogManager",
        "-Dnet.bytebuddy.experimental=true"
    ]
}

// ===== IDE Run Configurations =====

if (ext.hasHytaleHome) {
    def serverRunDir = file("$projectDir/run")
    
    // Ensure run directory exists
    serverRunDir.mkdirs()
    
    def createServerRunArguments = { String srcDir ->
        def args = "--allow-op --disable-sentry --assets=\"${project.ext.hytaleHome}/install/$patchline/package/game/latest/Assets.zip\""
        
        def modPaths = []
        if (includes_pack.toBoolean()) {
            modPaths << srcDir
        }
        if (load_user_mods.toBoolean()) {
            modPaths << "${project.ext.hytaleHome}/UserData/Mods"
        }
        
        if (!modPaths.isEmpty()) {
            args += " --mods=\"${modPaths.join(',')}\""
        }
        
        return args
    }
    
    // IntelliJ IDEA run configuration
    idea.project.settings.runConfigurations {
        'HytaleServer'(org.jetbrains.gradle.ext.Application) {
            mainClass = 'com.hypixel.hytale.Main'
            moduleName = project.idea.module.name + '.main'
            programParameters = createServerRunArguments(sourceSets.main.java.srcDirs.first().parentFile.absolutePath)
            workingDirectory = serverRunDir.absolutePath
        }
    }
    
    // VSCode launch configuration
    tasks.register('generateVSCodeLaunch') {
        def vscodeDir = file("$projectDir/.vscode")
        def launchFile = file("$vscodeDir/launch.json")
        
        doLast {
            vscodeDir.mkdirs()
            
            def launchConfig = [
                version: '0.2.0',
                configurations: [[
                    type: 'java',
                    name: 'HytaleServer',
                    request: 'launch',
                    mainClass: 'com.hypixel.hytale.Main',
                    args: createServerRunArguments('${workspaceFolder}'),
                    cwd: '${workspaceFolder}/run'
                ]]
            ]
            
            launchFile.text = groovy.json.JsonOutput.prettyPrint(groovy.json.JsonOutput.toJson(launchConfig))
            logger.lifecycle("VSCode launch configuration created at ${launchFile.path}")
        }
    }
} else {
    tasks.register('generateVSCodeLaunch') {
        doLast {
            logger.lifecycle("Skipped VSCode launch configuration because hytale_home is not set or the install path is missing.")
        }
    }
}
