plugins {
    id 'java'
    id 'maven-publish'
    id 'org.jetbrains.gradle.plugin.idea-ext' version '1.3'
}

version = project.version;

java {
    toolchain.languageVersion = JavaLanguageVersion.of(java_version)
    withSourcesJar()
    withJavadocJar()
}

javadoc {
    options.addStringOption('Xdoclint:-missing', '-quiet')
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    implementation("org.mozilla:rhino:1.9.0")
    implementation(files("/Users/federicoleiva/hytale/HytaleServer.jar"))
}

test {
    useJUnitPlatform()
}

jar {
    // Include Rhino dependency classes in the JAR
    from {
        configurations.runtimeClasspath.filter { 
            it.name.startsWith('rhino') 
        }.collect { zipTree(it) }
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

processResources {
    var expandProps = [
        'name'          : project.name,
        'version'       : project.version,
        'group'         : project.group,
        'description'   : mod_description,
        'website'       : website,
        'server_version': server_version,
        'entry_point'   : entry_point
    ]

    filesMatching(['manifest.json']) {
        expand expandProps
    }
    inputs.properties(expandProps)
}

def serverRunDir = file("$projectDir/run")

idea.project.settings.runConfigurations {
    'HytaleServer'(org.jetbrains.gradle.ext.Application) {
        mainClass = 'com.hypixel.hytale.Main'
        moduleName = project.idea.module.name + '.main'
        programParameters = "--allow-op --disable-sentry --assets=/Users/federicoleiva/hytale/Assets.zip --mods=${file("src/main/").absolutePath}  --auth-mode=authenticated"
        workingDirectory = serverRunDir.absolutePath
    }
}


publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
    if (providers.environmentVariable("HT_MAVEN_USERNAME").present) {
        repositories {
            maven() {
                url = "https://maven.hytale-modding.info/releases"
                credentials {
                    username = providers.environmentVariable("HT_MAVEN_USERNAME").get()
                    password = providers.environmentVariable("HT_MAVEN_PASSWORD").get()
                }
            }
        }
    }

}